plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = "${mod_version}"
group = 'edivad.fluidsystem'
archivesBaseName = "FluidSystem-${minecraft_version}"

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    if (mappings_channel == 'parchment_previous') {
        mappings channel: 'parchment', version: "${previous_minecraft_version}-${mappings_version}-${minecraft_version}"
    } else if (mappings_channel == 'parchment'){
        mappings channel: 'parchment', version: "${mappings_version}-${minecraft_version}"
    } else {
        mappings channel: 'official', version: "${minecraft_version}"
    }

    runs {
        client {
            workingDirectory project.file('run/client')

            // Recommended logging data for a userdev environment
            // property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', 'fluidsystem'

            mods {
                fluidsystem {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'fluidsystem'

            mods {
                fluidsystem {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'fluidsystem', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                fluidsystem {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        name 'tterrag'
        url 'https://maven.tterrag.com/'
    }
    maven {
        name 'TOP'
        url 'https://maven.k-4u.nl'
    }
    maven {
        name 'JEI'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'curseforge'
        url 'https://www.cursemaven.com'
    }
    flatDir {
        dir 'libs'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    //implementation fg.deobf("edivad:EdivadLib-${minecraft_version}:1.0.1")
    implementation fg.deobf("curse.maven:edivadlib-638508:${edivadlib_id}")

    implementation fg.deobf("mcjty.theoneprobe:theoneprobe:${top_version}") {
        transitive = false
    }

    compileOnly fg.deobf("curse.maven:jade-api-324717:${jade_id_api}")
    runtimeOnly fg.deobf("curse.maven:jade-324717:${jade_id}")
    //runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
    //runtimeOnly fg.deobf("mezz.jei:jei-1.19.3-forge:${jei_version}")

    //runtimeOnly fg.deobf("team.chisel.ctm:CTM:1.18.2-${ctm_version}")
    //runtimeOnly fg.deobf("curse.maven:mekanism-268560:${mekanism_version}")
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            'Specification-Title': 'FluidSystem',
            'Specification-Vendor': 'edivad',
            'Specification-Version': '1', // We are version 1 of ourselves
            'Implementation-Title': project.name,
            'Implementation-Version': project.jar.archiveVersion,
            'Implementation-Vendor' :'edivad',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

task apiJar(type: Jar) {
    classifier = 'api'
    from(sourceSets.main.output) {
        include 'edivad/fluidsystem/api/**'
    }
}

jar.finalizedBy('reobfJar')
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

artifacts {
    archives apiJar
}